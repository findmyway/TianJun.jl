---
keywords: FunctionalProgramming,Clojure
CJKmainfont: KaiTi
---

# Recursion

åœ¨clojureä¸­ï¼Œä¸»è¦é é€’å½’æ¥å®ç°å¾ªç¯æ§åˆ¶ç»“æ„ã€‚è®°å¾—åœ¨ã€Šcå’ŒæŒ‡é’ˆã€‹å…³äºé€’å½’å’Œè¿­ä»£è¿˜æœ‰è¿‡è¯¦ç»†ä»‹ç»ï¼Œæœ‰ä¸€äº›å°†äºŒè€…äº’æ¢çš„ç»ƒä¹ ï¼Œå¦‚ä½•å½¢è±¡åœ°ç†è§£äºŒè€…å¯ä»¥çœ‹[è¿™é‡Œ](https://www.zhihu.com/question/20278387)ã€‚ä»æ—¶é—´å¤æ‚åº¦ä¸Šæ¥è¯´ï¼ŒäºŒè€…æ˜¯ç­‰ä»·çš„ã€‚é€šå¸¸å°†å¾ªç¯ç»“æ„æ”¹å†™æˆé€’å½’ç»“æ„çš„ä¼ªä»£ç å¯ä»¥è¿™ä¹ˆå†™ï¼š

```
for(init, end-condition, change-state)
    do-something-here
    
f(state, & args)
    condition-meet?
        yes-do-something-and-return
        no-please-recur(state-update, do-something-with-args-and-return-updated-args)
```

å½“ç„¶ï¼Œé€’å½’è°ƒç”¨å¯ä»¥å‘ç”Ÿåœ¨å‡½æ•°ä½“å†…çš„ä»»ä½•åœ°æ–¹ï¼Œä¸è¿‡ï¼Œæœ‰ä¸€ç±»ç‰¹æ®Šçš„é€’å½’å‘ç”Ÿåœ¨å‡½æ•°è¿”å›çš„åœ°æ–¹ï¼Œç§°ä¸ºå°¾é€’å½’ï¼ˆTail Callï¼‰ã€‚æ­£æ˜¯å› ä¸ºå‘ç”Ÿåœ¨å‡½æ•°è¿”å›çš„åœ°æ–¹ï¼Œå‡½æ•°ä½“å†…çš„è¿è¡Œæ—¶ä¿¡æ¯ä¸å†éœ€è¦ä¿å­˜ï¼Œå› è€Œå¯ä»¥åšåˆ°å°¾é€’å½’ä¼˜åŒ–(TCO)ã€‚ä¸è¿‡ï¼Œå—é™äºJVMï¼Œclojureçš„å®ç°é‡Œï¼Œå¹¶ä¸èƒ½ç›´æ¥åšåˆ°TCOï¼Œä¸¾ä¾‹å¦‚ä¸‹ï¼š

```clojure
(defn f [n] 
  (if (> n 0)
    (f (dec n))
    0))

(f 100)     ; 0
(f 100000)  ; StackOverflowError   clojure.lang.Numbers$LongOps.combine (Numbers.java:419)
```

ä¸è¿‡åœ¨clojureä¸­ï¼Œæä¾›äº†ä¸€ç§ç±»ä¼¼gotoçš„è¯­æ³•ï¼Œå³[loopå’Œrecur](http://clojure.org/reference/special_forms#recur)æ¥å®ç°ä»å‡½æ•°å°¾éƒ¨è·³è½¬ã€‚å¦‚æœå‡½æ•°ä½“å†…æ²¡æœ‰æ˜¾å¼åœ°æä¾›loopè¯­å¥ï¼Œåˆ™ä¼šè·³è½¬åˆ°å‡½æ•°å¼€å§‹å¤„ã€‚è¿™é‡Œloopåœ¨è¯­ä¹‰ä¸Šå’Œletè¯­å¥æ˜¯ç­‰ä»·çš„ï¼Œåªä¸è¿‡æ·»åŠ äº†ä¸€ä¸ªæ ‡è®°æ–¹ä¾¿ç¼–è¯‘å™¨è¯†åˆ«ã€‚ä½¿ç”¨``recur``éœ€è¦æ³¨æ„çš„æ˜¯åªèƒ½å†™åœ¨å‡½æ•°çš„è¿”å›å¤„ï¼Œä¸èƒ½åƒæ™®é€šçš„é€’å½’ä¸€æ ·å¯¹é€’å½’è°ƒç”¨çš„ç»“æœåšäºŒæ¬¡å¤„ç†ã€‚

```clojure
(defn factorial [x]
  (if (= 1 x)
   1
   (* x (factorial (dec x)))))
;; it's ok, but...
(factorial 10000N)  ;; CompilerException java.lang.StackOverflowError

(defn factorial-recur1 [x]
  (if (= 1 x)
    1
    (* x (recur (dec x)))))
;; this version will not pass compile
;; CompilerException java.lang.UnsupportedOperationException: Can only recur from tail position

(defn factorial-recur2 [x]
  (loop [n x
         acc 1]
    (if (= 1 n)
      acc
      (recur (dec n) (* n acc)))))
```

æ¥ä¸‹æ¥è€ƒè™‘å¦ä¸€ç§æƒ…å†µï¼Œ**mutual recursion**ã€‚å‡è®¾å®šä¹‰äº†å¦‚ä¸‹ä¸¤ä¸ªå‡½æ•°``my-even?``å’Œ``my-odd?``ï¼š

```clojure
(declare my-odd? my-even?)    ;; make forward declarations first
(defn my-odd? [n]
      (if (= n 0)
          false
         (my-even? (dec n))))
(defn my-even? [n]
      (if (= n 0)
          true
         (my-odd? (dec n))))
         
(my-even? 10000)  ;; CompilerException java.lang.StackOverflowError
```

è¿™é‡Œï¼Œç”±äºåœ¨å‡½æ•°çš„ç»“å°¾å¤„æ˜¯ç›¸äº’é€’å½’è°ƒç”¨ï¼Œéœ€è¦ä¿å­˜å †æ ˆä¿¡æ¯ï¼Œå› æ­¤ä¼šé€ æˆStackOverflowï¼Œå‰é¢æˆ‘ä»¬å€Ÿç”¨äº†``loop``å’Œ``recur``å®ç°äº†ä¼˜åŒ–ï¼Œé‚£ä¹ˆè¿™é‡Œèƒ½å¦ç”¨åŒæ ·çš„å¥—è·¯å‘¢?ç»“æœæ˜¯ä¸èƒ½ï¼å› ä¸ºè¿™æ ·åšç›¸å½“äºä»ä¸€ä¸ªå‡½æ•°å†…éƒ¨gotoåˆ°äº†å¦ä¸€ä¸ªå‡½æ•°å†…éƒ¨ï¼Œ``recur``æ˜¾ç„¶æ˜¯ä¸ä¼šæ”¯æŒè¿™æ ·çš„æ“ä½œçš„ã€‚ä¸ºäº†è§£å†³è¿™ä¸ªé—®é¢˜ï¼Œclojureä¸­æä¾›äº†ä¸€ä¸ªæœ‰è¶£çš„å‡½æ•°``trampoline``ã€‚

```clojure
(defn trampoline
  ([f]
     (let [ret (f)]
       (if (fn? ret)
         (recur ret)
         ret)))
  ([f & args]
     (trampoline #(apply f args))))
```

å…¶æ€æƒ³å°±æ˜¯ï¼Œæ¯æ¬¡æ‰§è¡Œå®Œå‡½æ•°åï¼Œåˆ¤æ–­è¿”å›å€¼æ˜¯å¦ä»ç„¶æ˜¯å‡½æ•°ï¼Œå¦‚æœæ˜¯ï¼Œåˆ™recurç„¶åç»§ç»­æ‰§è¡Œï¼Œå¦åˆ™è¿”å›è¯¥å€¼ã€‚ç„¶åï¼Œæˆ‘ä»¬å¯ä»¥å°†``my-odd?``å’Œ``my-even?``æœ€åçš„å‡½æ•°äº’è°ƒç”¨å°è£…åœ¨ä¸€ä¸ª$\lambda$å‡½æ•°é‡Œï¼Œç”±``trampoline``å»æ‰§è¡Œã€‚ç”±äºè¿”å›å€¼æ˜¯ä¸€ä¸ª$\lambda$å‡½æ•°ï¼Œç›¸å½“äºç«‹å³è¿”å›äº†ï¼Œå› è€Œä¸ä¼šå†æœ‰StackOverflowErrorçš„é—®é¢˜ã€‚

```clojure
(declare my-even-helper? my-odd-helper?)  ;; make forward declarations first

(defn my-even-helper? [n]
  (if (zero? n)
    true
    #(my-odd-helper? (dec n))))

(defn my-odd-helper? [n]
  (if (zero? n)
    false
    #(my-even-helper? (dec n))))

(def my-even-new? (partial trampoline my-even-helper?))
(def my-odd-new? (partial trampoline my-odd-helper?))
```

é™¤äº†ç”¨æ¥è§£å†³mutual recursionçš„é—®é¢˜ä¹‹å¤–ï¼Œ``trampoline``è¿˜å¯ä»¥å¾ˆä¼˜é›…åœ°ç”¨äºè§£å†³æœ‰é™çŠ¶æ€æœºçš„é—®é¢˜ï¼Œå…·ä½“å¯ä»¥å‚è€ƒä¸€äº›[è¿™é‡Œ](http://clojuredocs.org/clojure.core/trampoline)çš„ä¾‹å­.

# Memoize

å‰é¢åªæ˜¯å¯¹recursionåšäº†ä¸ªç®€å•çš„å›é¡¾ï¼Œæ¥ä¸‹æ¥èŠä¸€ä¸ªè‡ªå·±å†™ä»£ç è¿‡ç¨‹ä¸­å®é™…é‡åˆ°çš„é—®é¢˜ã€‚

é—®é¢˜æ˜¯è¿™æ ·å­çš„ï¼Œ[Collatz Conjecture][]çš„ç®€å•æè¿°å¦‚ä¸‹ï¼š

> ç»™å®šä»»æ„ä¸€ä¸ªæ­£æ•´æ•°ï¼š1) å¦‚æœè¿™ä¸ªæ•°æ˜¯å¶æ•°ï¼Œåˆ™å¯¹å®ƒé™¤ä»¥2ï¼›2) å¦‚æœè¿™ä¸ªæ•°æ˜¯å¥‡æ•°ï¼Œåˆ™å¯¹å®ƒä¹˜ä»¥3ä»¥ååŠ 1ã€‚å¦‚æ­¤å¾ªç¯ä¸‹å»ï¼Œæœ€åéƒ½èƒ½å¤Ÿå¾—åˆ°1ã€‚

æ‰¾åˆ°1000000ä»¥å†…çš„æŸä¸ªæ•°ä½¿å¾—å…¶æ”¶æ•›åˆ°1çš„æ­¥éª¤æœ€é•¿ã€‚

è¿™é‡Œå…ˆä¸åšè¿‡å¤šçš„æ•°å­¦åˆ†æï¼Œå…ˆçœ‹ä¸€ä¸ªæœ€naiveçš„ç‰ˆæœ¬ï¼š

```clojure
(defn collatz-cnt [x]
  (loop [x x c 1]
    (if (= 1 x)
      c
      (recur (if (even? x) 
               (/ x 2)
               (inc (* 3 x)))
             (inc c)))))
             
(time (reduce #(let [c (collatz-cnt %2)]
                 (if (> c (first %1))
                   [c %2]
                   %1))
              [0 0]
              (range 1 1000000 2)))
;"Elapsed time: 8867.352869 msecs"
;[525 837799]
```

è¿™ä¹ˆåšæ˜¾ç„¶å¾ˆè€—æ—¶ï¼Œé€šè¿‡ç®€å•åˆ†æå¯ä»¥çœ‹å‡ºï¼Œéå†æ±‚``collatz-cnt``çš„è¿‡ç¨‹ä¸­ï¼Œä¼šæœ‰å¤§é‡çš„é‡å¤è®¡ç®—ã€‚å¦‚æœèƒ½æŠŠä¸­é—´ç»“æœç¼“å­˜èµ·æ¥ï¼Œé‚£ä¹ˆåº”è¯¥èƒ½å‡å°‘å¾ˆå¤šè®¡ç®—é‡ã€‚clojureä¸­æä¾›äº†ä¸€ä¸ª``memoize``å‡½æ•°ä¸“é—¨ç”¨æ¥ç¼“å­˜å‡½æ•°è°ƒç”¨çš„ä¸­é—´ç»“æœï¼Œä¸Python3ä¸­``functools.lru_cache``æœ‰ç‚¹ç±»ä¼¼ï¼ˆä¸è¿‡æ²¡æœ‰lruï¼‰ã€‚å…¶[å®ç°](https://github.com/clojure/clojure/blob/010864f8ed828f8d261807b7345f1a539c5b20df/src/clj/clojure/core.clj#L6097)å¦‚ä¸‹ï¼š

```clojure
(defn memoize
  [f]
  (let [mem (atom {})]
    (fn [& args]
      (if-let [e (find @mem args)]
        (val e)
        (let [ret (apply f args)]
          (swap! mem assoc args ret)
          ret)))))
```

å†…éƒ¨å®é™…ä¸Šå°±æ˜¯é€šè¿‡``(atom {})``æ¥å®ç°ç¼“å­˜çš„ï¼Œäºæ˜¯ï¼Œæˆ‘å…ˆç®€å•åœ°å†™äº†ä¸ªç¼“å­˜çš„ç‰ˆæœ¬ï¼š

```clojure
(def collatz-cnt-memo (memoize collatz-cnt))

(time (reduce #(let [c (collatz-cnt-memo %2)]
                 (if (> c (first %1))
                   [c %2]
                   %1))
              [0 0]
              (range 1 1000000 2)))
;"Elapsed time: 8916.976351 msecs"
;[525 837799]
```

ä¸è¿‡è·‘å®Œæ‰å‘ç°ï¼Œå‹æ ¹æ²¡æœ‰ç¼“å­˜ï¼Œä»ç„¶æ˜¯é‚£ä¹ˆæ…¢ï¼Œä»”ç»†åˆ†æäº†ä¸‹ï¼Œæ˜¯å› ä¸ºç¼“å­˜çš„æ—¶å€™åªå¯¹``collatz-cnt-memo``çš„å‚æ•°åšäº†ç¼“å­˜ï¼Œå¹¶æ²¡æœ‰å¯¹``collatz-cnt``å‡½æ•°çš„å‚æ•°åšç¼“å­˜ã€‚äºæ˜¯ï¼Œå†™å‡ºäº†å¦ä¸€ä¸ªç‰ˆæœ¬ï¼š

```clojure
(def collatz-cnt-memo2 
  (memoize (fn [x]
             (if (= 1 x)
               1
               (if (even? x) 
                 (inc (collatz-cnt-memo2 (/ x 2)))
                 (inc (collatz-cnt-memo2 (inc (* 3 x)))))))))
```

çœ‹èµ·æ¥å¾ˆå®Œç¾ï¼Œæ¯æ¬¡è°ƒç”¨å‡½æ•°çš„æ—¶å€™ï¼Œåœ¨å°¾éƒ¨é€’å½’è°ƒç”¨è‡ªå·±ï¼Œè€Œ``collatz-cnt-memo2``å‡½æ•°åˆæ˜¯ç¼“å­˜äº†çš„ï¼Œæ•ˆç‡åº”è¯¥æå‡å¾ˆå¤šã€‚ç­‰ç­‰ï¼Œä¼¼ä¹ï¼Œè¿™ä¸ªå¹¶ä¸æ˜¯å°¾é€’å½’è°ƒç”¨ï¼Œä¼šä¸ä¼š......``StackOverflowError``!!!è¯•è¯•``(collatz-cnt-memo2 837799)``æœç„¶å¦‚æ­¤ã€‚é‚£ï¼Œèƒ½å¦ç”¨recuræ¥æ›¿æ¢æ‰å†…éƒ¨çš„é€’å½’å‘¢ï¼Ÿæˆ‘è‡ªå·±è¯•äº†ä¸‹ï¼Œå‡ ä¹å¾ˆéš¾åŒæ—¶ç”¨``recur``å’Œ``memoize``æ¥å®ç°ï¼Œæ¯”è¾ƒæ¥è¿‘ä¸€ç‚¹çš„å®ç°æ˜¯ä¸ä½¿ç”¨``memoize``å‡½æ•°ï¼Œè€Œæ˜¯è‡ªå·±ç”¨å®å®ç°ä¸€ä¸ªç±»ä¼¼çš„ç¼“å­˜æœºåˆ¶ï¼ˆè¿™æ ·çš„åšæ³•æ˜¾ç„¶ä¸å¤Ÿä¼˜é›…ï¼‰ã€‚è”æƒ³åˆ°å‰é¢çš„``trampoline``å‡½æ•°ï¼Œå¯ä»¥å°è¯•å†™å‡ºè¿™æ ·çš„ç‰ˆæœ¬ï¼š

```clojure
(def collatz-cnt-memo3 
  (memoize (fn [x c]
             (if (= 1 x)
               c
               (if (even? x) 
                 #(collatz-cnt-memo3 (/ x 2) (inc c))
                 #(collatz-cnt-memo3 (inc (* 3 x)) (inc c)))))))
(def collatz-cnt-memo3 (partial trampoline collatz-cnt-memo3))
```

ä¼¼ä¹ï¼Œè§£å†³äº†``StackOverflow``çš„é—®é¢˜ï¼Œç„¶è€Œï¼Œè¿™ä¸ªå‡½æ•°å¹¶æ²¡æœ‰å®ç°çœŸæ­£æ„ä¹‰ä¸Šçš„ç¼“å­˜ï¼Œå› ä¸ºå‡½æ•°å†…éƒ¨è¿­ä»£çš„æ—¶å€™ï¼Œä¼ å…¥äº†ä¸¤ä¸ªå‚æ•°``x``å’Œ``c``ï¼Œæ˜¾ç„¶æˆ‘ä»¬å¸Œæœ›ç¼“å­˜çš„æ˜¯``fn [x]``è€Œä¸æ˜¯``fn [x c]``ï¼Œä½†æ˜¯ï¼Œå¦‚æœåªä¼ ä¸€ä¸ªå‚æ•°``x``ï¼Œåˆæ²¡æ³•åšåˆ°å°¾é€’å½’ï¼ˆæœ€åéœ€è¦ä¸€ä¸ªincæ“ä½œé€’å½’çš„è¿”å›å€¼ï¼Œä½¿å¾—count + 1ï¼‰ï¼Œä¼¼ä¹ï¼Œé™·å…¥äº†ä¸€ä¸ªæ€ªåœˆ......

å…³äºå¦‚ä½•ä¸€æ­¥æ­¥ä¼˜åŒ–è¿™ä¸ªé—®é¢˜çš„clojureä»£ç ï¼Œå¯ä»¥çœ‹[è¿™é‡Œ](http://www.petrounias.org/articles/2014/08/03/collatz-sequence-generation-performance-profiling-in-clojure/)(ç¡®å®è¦æ¯”C++å’Œpythonçš„ä»£ç éƒ½è¦æ…¢å¾ˆå¤š)ã€‚æ¥ä¸‹æ¥è½¬å‘å¦å¤–ä¸€ä¸ªè¯é¢˜ã€‚


# Y Combinator

çœ‹äº†ä¸€ä¸ªå‘¨æœ«ï¼Œå¯¹è‡ªå·±çš„æ™ºå•†äº§ç”Ÿäº†æ€€ç–‘......ğŸ˜‚ğŸ˜‚ğŸ˜‚æ„Ÿè§‰ï¼Œç†è§£äº†æ˜¯æ€ä¹ˆå›äº‹ï¼Œå¹¶æ²¡æœ‰ä½“ä¼šåˆ°å…¶ä¸­çš„ç²¾é«“ï¼Œå»ºè®®çœ‹çœ‹[wikipedia](https://en.wikipedia.org/wiki/Fixed-point_combinator)ã€‚


# Reference



- [Trampolining through mutual recursion with Clojure](http://jakemccrary.com/blog/2010/12/06/trampolining-through-mutual-recursion/)
- [Clojure Doc - trampoline](http://clojuredocs.org/clojure.core/trampoline)
- [è¯¦è§£Clojureçš„é€’å½’(ä¸Šï¼‰â€”â€” ç›´æ¥é€’å½’åŠä¼˜åŒ–](http://www.blogjava.net/killme2008/archive/2010/07/14/326129.html)
- [è¯¦è§£Clojureçš„é€’å½’ï¼ˆä¸‹ï¼‰â€”â€”ç›¸äº’é€’å½’å’Œtrampoline](http://www.blogjava.net/killme2008/archive/2010/08/22/329576.html)
- [In Clojure, is it possible to combine memoization and tail call optimization?](http://stackoverflow.com/questions/9898069/in-clojure-is-it-possible-to-combine-memoization-and-tail-call-optimization)
- [How do I generate memoized recursive functions in Clojure?](http://stackoverflow.com/questions/3906831/how-do-i-generate-memoized-recursive-functions-in-clojure)
- [Recursions without names: Introduction to the Y combinator in clojure](http://blog.klipse.tech/lambda/2016/08/07/almost-y-combinator-clojure.html)
- [The Y combinator in clojure](http://blog.klipse.tech/lambda/2016/08/07/pure-y-combinator-clojure.html)
- [Y combinator real life application: recursive memoization in clojure](http://blog.klipse.tech/lambda/2016/08/10/y-combinator-app.html)
- [The Y Combinator (Slight Return)](http://mvanier.livejournal.com/2897.html)

[Collatz Conjecture]:https://en.wikipedia.org/wiki/Collatz_conjecture